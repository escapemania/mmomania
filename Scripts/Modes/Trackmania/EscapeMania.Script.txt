// #RequireContext CSmMode

#Extends "Libs/Nadeo/TMNext/TrackMania/Modes/TMNextBase.Script.txt"

#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Rooms/Rooms.Script.txt" as Rooms
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Rooms/RoomsRequestRespawn.Script.txt" as RoomsRequestRespawn
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Timer/Timer.Script.txt" as Timer
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/MediaTracker/MediaTracker.Script.txt" as MediaTracker

#Include "Libs/EscapeMania/ModeLibs/EscapeMania/EnergyWall/EnergyWall.Script.txt" as EnergyWall
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/XPuzzle/XPuzzle.Script.txt" as XPuzzle
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Simon/Simon.Script.txt" as Simon
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Pipe/Pipe.Script.txt" as Pipe
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Water/Water.Script.txt" as Water
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Turtle/Turtle.Script.txt" as Turtle
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Tunnel/TunnelMedia.Script.txt" as TunnelMedia
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Tunnel/TunnelItem.Script.txt" as TunnelItem
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Wordle/Wordle.Script.txt" as Wordle
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Challenge/Challenge.Script.txt" as Challenge
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/ControlRoom/ControlRoom.Script.txt" as ControlRoom

#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Items/Items.Script.txt" as Items
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Hints/Hints.Script.txt" as Hints
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Collisions/Collisions.Script.txt" as Collisions
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Language/Language.Script.txt" as Language

#Include "Libs/Zrx/ModeLibs/Common/VirtualPickups/Inventory.Script.txt" as Inventory
#Include "Libs/Zrx/ModeLibs/Common/Commands/ModeCommands.Script.txt" as ModeCommands
#Include "Libs/Zrx/ModeLibs/Common/VirtualPickups/VirtualPickups.Script.txt" as VirtualPickups

#Include "TextLib" as TL
#Include "MathLib" as ML

#Const	CompatibleMapTypes						"Trackmania\\TM_Race,TM_Race,Trackmania\\EscapeManiaArena,EscapeManiaArena"
#Const	Version												"0.0.1"
#Const	ScriptName										"Modes/Trackmania/EscapeMania.Script.txt"

#Const C_Layer_Inventory "Layer_Inventory"
#Const C_LayerName_ModeCommands "Layer_ModeCommands"
#Const C_LayerName_Timer "Layer_Timer"
#Const C_LayerName_ItemsPreload "Layer_ItemsPreload"

#Const C_HintLandmarkTag "Hint"

***Match_StartServer***
***
Race::UseCppRaceProgression(False);

ModeCommands::AddAdmin("KTaOsd-lTR2zkoskETSfPA"); // vjeux
ModeCommands::AddAdmin("W2ZNWNzVQV6z3hXuNgRMRg"); // Ze-Rax

Layers::Create(C_Layer_Inventory, Inventory::GetManialink());
Layers::SetType(C_Layer_Inventory, CUILayer::EUILayerType::Normal);
Layers::Attach(C_Layer_Inventory);

Layers::Create(C_LayerName_ModeCommands, ModeCommands::GetManialink());
Layers::SetType(C_LayerName_ModeCommands, CUILayer::EUILayerType::Normal);
Layers::Attach(C_LayerName_ModeCommands);

Layers::Create(C_LayerName_Timer, Timer::GetManialink());
Layers::SetType(C_LayerName_Timer, CUILayer::EUILayerType::Normal);
Layers::Attach(C_LayerName_Timer);

Layers::Create(C_LayerName_ItemsPreload, Items::GetManialink());
Layers::SetType(C_LayerName_ItemsPreload, CUILayer::EUILayerType::Normal);
Layers::Attach(C_LayerName_ItemsPreload);

Collisions::OnStartServer();
***

***Match_StartMap***
***
Rooms::OnStartMap();
***

***Match_StartPlayLoop***
***
Timer::Init();
Items::Init();
EnergyWall::Init();
XPuzzle::Init();
Simon::Init();
Water::Init();
Turtle::Init();
TunnelMedia::Init();
TunnelItem::Init();
Wordle::Init();
ControlRoom::Init();
Challenge::Init();
Hints::Init();
Language::Init();
***

Void OnRespawn(CSmPlayer Player, CMapLandmark Location) {
	Water::OnRespawn(Player);
	EnergyWall::OnRespawn(Player);
	Rooms::OnRespawn(Player, Location);
	Turtle::OnRespawn(Player);
	Wordle::OnRespawn(Player);
	TunnelMedia::OnRespawn(Player);
	TunnelItem::OnRespawn(Player);
	Challenge::OnRespawn(Player, Location);
	MediaTracker::OnRespawn(Player);
}

Void OnPlayerSpawn(CSmPlayer Player) {
	Rooms::OnPlayerSpawn(Player);
	EnergyWall::OnPlayerSpawn(Player);
	MediaTracker::OnPlayerSpawn(Player);
	ControlRoom::OnPlayerSpawn(Player);
	Collisions::OnPlayerSpawn(Player);
}

Void OnPlayerLeave(CUser User) {
	Challenge::OnPlayerLeave(User);
	TunnelMedia::OnPlayerLeave(User);
}

Void OnHint() {
	Hints::SendHint([
		Rooms::GetHintsRoom0(),
		EnergyWall::GetHints(),
		Simon::GetHints(),
		Pipe::GetHints(),
		Water::GetHints(),
		XPuzzle::GetHints(),
		Rooms::GetHintsRoom1(),
		Challenge::GetHints(),
		Wordle::GetHints(),
		Turtle::GetHints(),
		TunnelMedia::GetHints(),
		TunnelItem::GetHints(),
		ControlRoom::GetHints(),
		Rooms::GetHintsRoom2()
	]);
}

***Match_PlayLoop***
***
foreach (Player in Players) {
	if (Player.SpawnStatus == CSmPlayer::ESpawnStatus::NotSpawned) {
		OnPlayerSpawn(Player);
	}
}

foreach (Event in PendingEvents) {
	switch (Event.Type) {
		case CSmModeEvent::EType::OnPlayerRequestRespawn: {
			OnRespawn(Event.Player, Null);
		}
		case CSmModeEvent::EType::OnPlayerAdded: {	
			UIManager.UIAll.SendChat(Event.Player.User.Name ^ " joined the Escape!");
		}
		case CSmModeEvent::EType::OnPlayerRemoved: {
			OnPlayerLeave(Event.User);
			UIManager.UIAll.SendChat(Event.User.Name ^ " left the Escape!");
		}
		case CSmModeEvent::EType::OnPlayerTriggersWaypoint: {
			if (Event.Landmark.Tag == C_HintLandmarkTag) {
				OnHint();
			}
		}
	}
}


foreach (Command in ModeCommands::GetPendingCommands()) {
  switch (Command.Name) {
		case "Command_hint": {
			OnHint();
		}
    case "Command_bot": {
			Users_SetNbFakeUsers(Command.ValueInteger, 0);
    }
    case "Command_message": {
			UIManager.UIAll.SendChat(Command.ValueText);
			UIManager.UIAll.QueueMessage(3000, 1, CUIConfig::EMessageDisplay::Big, Command.ValueText);
    }
		case "Command_cam7": {
			if (Command.ValueText == "enable") {
				UIManager.UIAll.PlayerDisableFreeCam = False;
			} else if (Command.ValueText == "disable") {
				UIManager.UIAll.PlayerDisableFreeCam = True;
			}
		}
    case "Command_complete": {
			switch (Command.ValueText) {
				case "x": XPuzzle::CompletePuzzle();
				case "pipe": Pipe::CompletePuzzle();
				case "energy": EnergyWall::CompletePuzzle();
				case "simon": Simon::CompletePuzzle();
				case "water": Water::CompletePuzzle();
				case "room1": foreach (Player in Players) Rooms::EnterRoom(Player, 1);
				case "room2": foreach (Player in Players) Rooms::EnterRoom(Player, 2);
				case "turtle": Turtle::CompletePuzzle();
				case "wordle": Wordle::CompletePuzzle();
				case "tunnel": TunnelItem::CompletePuzzle();
				case "controlroom": ControlRoom::CompletePuzzle();
				case "badge": ControlRoom::CompleteBadge();
				case "challenge": Challenge::CompletePuzzle();
			}
    }
    case "Command_reset": {
			switch (Command.ValueText) {
				case "x": XPuzzle::ResetPuzzle();
				case "pipe": Pipe::ResetPuzzle();
				case "energy": EnergyWall::ResetPuzzle();
				case "simon": Simon::ResetPuzzle();
				case "water": Water::ResetPuzzle();
				case "room1": Rooms::ResetPuzzle(0);
				case "room2": Rooms::ResetPuzzle(1);
				case "turtle": Turtle::ResetPuzzle();
				case "wordle": Wordle::ResetPuzzle();
				case "tunnel": TunnelItem::ResetPuzzle();
				case "controlroom": ControlRoom::ResetPuzzle();
				case "badge": ControlRoom::ResetBadge();
				case "challenge": Challenge::ResetPuzzle();
				case "timer": Timer::ResetPuzzle();
			}
    }
		case "Command_additem": {
			foreach (Player in Players) {
				switch (Command.ValueText) {
					case "water": {
						Items::CreateHelmetItem(Player);
						Items::CreatePowerSourceItem(Player);
					}
					case "powersource": Items::CreatePowerSourceItem(Player);
					case "helmet": Items::CreateHelmetItem(Player);
					case "u": Items::CreateLetterUItem(Player);
					case "i": Items::CreateLetterIItem(Player);
					case "l": Items::CreateLetterLItem(Player);
					case "n": Items::CreateLetterNItem(Player);
					case "orb": Items::CreateOrbItem(Player);
					case "bottle": Items::CreateBottleItem(Player);
					case "key": Items::CreateKeyItem(Player);
					case "badge": Items::CreateBadgeItem(Player);
				}
			}
		}
		case "Command_restart": {
			assert(False);
		}
  }
}

Timer::Loop();
Rooms::Loop();
EnergyWall::Loop();
XPuzzle::Loop();
Inventory::Loop();
Simon::Loop();
Pipe::Loop();
Water::Loop();
Turtle::Loop();
TunnelMedia::Loop();
TunnelItem::Loop();
Wordle::Loop();
ControlRoom::Loop();
Challenge::Loop();
Language::Loop();

foreach (Player => HasRequest in RoomsRequestRespawn::GetRequests()) {
	if (HasRequest) {
		OnRespawn(Player, RoomsRequestRespawn::GetRequestsLocation().get(Player, Null));
	}
}
RoomsRequestRespawn::Reset();
***


***Match_Yield***
***
ModeCommands::Yield();
VirtualPickups::Update();
***


Void DoNothing() {}
